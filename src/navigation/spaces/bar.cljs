(ns navigation.spaces.bar
  (:require
   [re-frame.core :as re-frame]
   [reagent.core :as r]
   ["react-virtuoso" :refer [Virtuoso]]
   [cljs-workers.core :as main]
   [client.state :as state]
   [cljs.core.async :refer [go <!]]
   [overlays.settings :refer [sidebar-profile-mini]]
   [utils.svg :as icons]
   [utils.helpers :refer [mxc->url]]
   [utils.global-ui :refer [avatar]]))

(re-frame/reg-event-db
 :sdk/set-spaces-list-sync
 (fn [db [_ spaces-vec]]
   (let [spaces-map (into {} (for [s spaces-vec :when (:id s)] [(:id s) s]))]
     (-> db
         (assoc :spaces-list spaces-vec)
         (assoc :spaces spaces-map)))))

(re-frame/reg-event-db
 :sdk/update-space-view
 (fn [db [_ space-id space-items]]
   (let [children-refs (mapv (fn [item]
                               {:id        (:id item)
                                :is-space? (:is-space? item)})
                             space-items)]
     (assoc-in db [:space-children space-id] children-refs))))

(re-frame/reg-event-fx
 :sdk/paginate-space
 (fn [_ [_ space-id]]
   (main/do-with-pool! @state/!engine-pool {:handler :paginate-space
                                            :arguments {:space-id space-id}})
   {}))

(re-frame/reg-event-db
 :space/process-hierarchy
 (fn [db [_ root-space-id rooms]]
   (let [hierarchy-orders
         (reduce
          (fn [acc room]
            (let [parent-id       (:room_id room)
                  children-events (:children_state room)]
              (if (seq children-events)
                (let [child-map (reduce
                                 (fn [cmap event]
                                   (if (= (:type event) "m.space.child")
                                     (let [child-id (:state_key event)
                                           order    (get-in event [:content :order] "zzzz")]
                                       (assoc cmap child-id order))
                                     cmap))
                                 {} children-events)]
                  (assoc acc parent-id child-map))
                acc)))
          {} rooms)]
     (update db :space-orders merge hierarchy-orders))))

(re-frame/reg-event-fx
 :sdk/fetch-space-hierarchy
 (fn [_ [_ space-id]]
   (go
     (let [pool @state/!engine-pool
           res  (<! (main/do-with-pool! pool {:handler :fetch-space-hierarchy
                                              :arguments {:space-id space-id}}))]
       (when (= (:status res) "success")
         (re-frame/dispatch [:space/process-hierarchy space-id (:rooms res)]))))
   {}))

(re-frame/reg-event-fx
 :space/select
 (fn [{:keys [db]} [_ space-id]]
   (let [current-space (:active-space-id db)]
     (if (= current-space space-id)
       {}
       {:db (-> db
                (assoc :active-space-id space-id)
                (assoc :active-room-id nil))
        :dispatch-n (if space-id
                      [[:sdk/paginate-space space-id]
                       [:sdk/fetch-room-emotes :space space-id]
                       [:sdk/fetch-space-hierarchy space-id]
                       [:room-list/apply-filter "all"]]
                      [[:room-list/apply-filter "people"]])}))))



(re-frame/reg-sub :spaces/all (fn [db _] (get db :spaces-list [])))
(re-frame/reg-sub :spaces/active-id (fn [db _] (:active-space-id db)))
(re-frame/reg-sub :space-children/map (fn [db _] (:space-children db)))

(re-frame/reg-sub
 :spaces/active-metadata
 :<- [:spaces/active-id]
 :<- [:spaces/all]
 (fn [[active-id all-spaces] _]
   (when active-id
     (some #(when (= (:id %) active-id) %) all-spaces))))

(re-frame/reg-sub
 :space/children
 :<- [:space-children/map]
 :<- [:rooms/all]
 (fn [[rel-map all-rooms] [_ parent-id]]
   (let [child-refs (get rel-map parent-id [])
         rich-lookup (into {} (map (fn [r] [(or (:id r) (.-roomId r)) r]) all-rooms))]
     (mapv (fn [{:keys [id is-space?]}]
             (let [rich-room (get rich-lookup id)]
               (merge {:id id :is-space? is-space? :name "Loading..."}
                      rich-room)))
           child-refs))))

(defn space-icon-item [item active-id]
  (let [{:keys [id name avatar-url is-dm?]} item
        stats (if is-dm? item @(re-frame/subscribe [:space/unread-stats id]))
        {:keys [unread? highlight? mentions msg-count]} stats
        badge-count (if is-dm? msg-count mentions)

        profile          @(re-frame/subscribe [:sdk/profile])
        my-id            (:user-id profile)
        needs-members?   is-dm?
        members-map      (when is-dm? @(re-frame/subscribe [:room/members-map id]))
        members-loading? (when is-dm? @(re-frame/subscribe [:room/members-loading? id]))]

    (when (and needs-members? (not members-loading?) (empty? members-map))
      (re-frame/dispatch [:room/fetch-members id]))

    (let [other-user   (when (and is-dm? (seq members-map))
                         (->> (vals members-map)
                              (remove #(= (:user-id %) my-id))
                              first))
          final-avatar (or (mxc->url avatar-url) (:avatar-url other-user))
          final-name   (if (and is-dm? other-user (= name "Loading..."))
                         (:display-name other-user)
                         name)]

      [:div.space-icon-wrapper
       {:class [(when (= id active-id) "active")
                (when unread? "has-unread")
                (when highlight? "has-highlight")
                (when is-dm? "is-dm-entry")]
        :on-click #(if is-dm?
                     (do
                       (re-frame/dispatch [:space/select nil])
                       (re-frame/dispatch [:rooms/select id]))
                     (re-frame/dispatch [:space/select id]))
        :title final-name}
       (when unread? [:div.space-notification-pill])
       [avatar {:id id :name final-name :url final-avatar :size 42 :shape :squircle}]
       (when (and badge-count (pos? badge-count))
         [:div.space-mention-badge badge-count])])))

(defn virtualized-space-bar [spaces active-space-id]
  (let [space-array (to-array spaces)]
    [:> Virtuoso
     {:style {:height "100%" :width "100%"}
      :data space-array
      :itemContent
      (fn [index space-map]
        (r/as-element
         ^{:key (:id space-map)}
         [space-icon-item space-map active-space-id]))}]))




(defn spaces-sidebar []
  (let [spaces        @(re-frame/subscribe [:spaces/all])
        active-id     @(re-frame/subscribe [:spaces/active-id])
        active-dms    @(re-frame/subscribe [:rooms/active-dm-pops])
        tr            @(re-frame/subscribe [:i18n/tr])]
    [:div.sidebar-spaces
     [:div.home-group
      [:div.space-icon-wrapper
       {:class (when (nil? active-id) "active")
        :on-click #(re-frame/dispatch [:space/select nil])
        :title (tr [:navigation.spaces.bar/home])}
       [:div.home-icon-container [icons/sun {:animate :sun-pulse :size "32px"}]]]
      (for [dm active-dms]
        ^{:key (:id dm)}
        [space-icon-item dm active-id])]
     [:div.sidebar-divider]
     [:div.spaces-list-container
      [virtualized-space-bar spaces active-id]]
     [sidebar-profile-mini]]))
