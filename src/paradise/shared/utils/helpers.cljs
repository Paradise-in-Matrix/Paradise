(ns paradise.shared.utils.helpers
  (:require
   [net :as net]
   [paradise.media.component :refer [media media-native]]
   [clojure.string :as str]
   [promesa.core :as p]
   [hickory.core :as h]
   [hickory.render :as hr]
   [clojure.walk :as walk]
   [taoensso.timbre :as log]
   ))

(def max-tag-nesting 100)

(def permitted-tags
  #{:font :del :h1 :h2 :h3 :h4 :h5 :h6 :blockquote :p :a :ul :ol :sup :sub
    :li :b :i :u :strong :em :strike :s :code :hr :br :div :table :thead
    :tbody :tr :th :td :caption :pre :span :img :details :summary})

(def url-schemes #{"https" "http" "ftp" "mailto" "magnet"})

(def permitted-attrs
  {:font #{:style :data-mx-bg-color :data-mx-color :color}
   :span #{:style :data-mx-bg-color :data-mx-color :data-mx-spoiler :data-mx-maths :data-mx-pill :data-mx-ping :data-md}
   :div  #{:data-mx-maths}
   :blockquote #{:data-md}
   :h1 #{:data-md} :h2 #{:data-md} :h3 #{:data-md} :h4 #{:data-md} :h5 #{:data-md} :h6 #{:data-md}
   :pre  #{:data-md :class}
   :ol   #{:start :type :data-md}
   :ul   #{:data-md}
   :a    #{:name :target :href :rel :data-md}
   :img  #{:width :height :alt :title :src :data-mx-emoticon :class :className}

   :code #{:class :data-md}
   :strong #{:data-md} :i #{:data-md} :em #{:data-md} :u #{:data-md} :s #{:data-md} :del #{:data-md}
   })


(def non-text-tags #{:style :script :textarea :option :noscript :mx-reply})


(defn- parse-style-str
  "Converts 'color: red; margin-top: 10px' into {:color 'red' :margin-top '10px'}"
  [style-str]
  (if (string? style-str)
    (->> (str/split style-str #";")
         (remove str/blank?)
         (map #(str/split % #":"))
         (filter #(= 2 (count %)))
         (map (fn [[k v]] [(keyword (str/trim k)) (str/trim v)]))
         (into {}))
    style-str))

(defn- transform-font-span [attrs]
  (let [bg (get attrs :data-mx-bg-color)
        fg (get attrs :data-mx-color)
        existing-style (parse-style-str (get attrs :style))
        style-map (cond-> (or existing-style {})
                    bg (assoc :background-color bg)
                    fg (assoc :color fg))]
    (if (seq style-map)
      (assoc attrs :style style-map)
      attrs)))

(defn- transform-a [attrs]
  (assoc attrs :rel "noopener" :target "_blank"))

(defn- transform-img [attrs]
  (let [src (get attrs :src "")]
    (if (and (string? src) (str/starts-with? src "mxc://"))
      {:tag "comp:paradise.media.component/media-native"
       :attrs {:mxc   src
               :class "timeline-emotes"
               :className "timeline-emotes"
               :alt   (get attrs :alt)}}
      {:tag :a
       :attrs {:href src :rel "noopener" :target "_blank"}
       :content [(or (get attrs :alt) src)]})))




(defn- filter-code-classes [attrs]
  (if-let [cls (:class attrs)]
    (let [classes (str/split cls #"\s+")
          valid (filter #(str/starts-with? % "language-") classes)]
      (if (seq valid)
        (assoc attrs :class (str/join " " valid))
        (dissoc attrs :class)))
    attrs))

(defn- valid-url? [href]
  (if (string? href)
    (let [scheme (-> href (str/split #":" 2) first str/lower-case)]
      (contains? url-schemes scheme))
    false))

(defn sanitize-nodes [nodes depth]
  (if (> depth max-tag-nesting)
    []
    (mapcat
     (fn [node]
       (cond
         (string? node)
         [node]

         (and (map? node) (= (:type node) :element))
         (let [{:keys [tag attrs content]} node]
           (cond
             (contains? non-text-tags tag)
             []

             (contains? permitted-tags tag)
             (let [allowed-keys (get permitted-attrs tag #{})
                   clean-attrs (select-keys attrs allowed-keys)
                   [final-tag final-attrs transformed-content]
                   (case tag
                     (:font :span) [tag (transform-font-span clean-attrs) nil]
                     :a            [tag (transform-a clean-attrs) nil]
                     :code         [tag (filter-code-classes clean-attrs) nil]
                     :img          (let [{t :tag a :attrs c :content} (transform-img clean-attrs)]
                                     [t a c])
                     [tag clean-attrs nil])
                   final-attrs (if (and (= final-tag :a)
                                        (not (valid-url? (:href final-attrs))))
                                 (dissoc final-attrs :href)
                                 final-attrs)
                   children (or transformed-content
                                (sanitize-nodes content (inc depth)))]
               [(if (seq final-attrs)
                  (into [final-tag final-attrs] children)
                  (into [final-tag] children))])

             :else
             (sanitize-nodes content depth)))

         :else []))
     nodes)))

(defn sanitize-custom-html [raw-html]
  (when raw-html
    (let [html-str (str raw-html)
          raw-fragments (h/parse-fragment html-str)
          hickory-maps  (map h/as-hickory raw-fragments)]
      (sanitize-nodes hickory-maps 0))))

(defn sanitize-text [raw-text]
  (when raw-text
    (str/escape (str raw-text) {\& "&amp;" \< "&lt;" \> "&gt;" \" "&quot;" \' "&#39;"})))


(defn linkify-text [text]
  (if (str/blank? text)
    [text]
    (let [pattern #"https?://[^\s]+"
          parts (str/split text pattern -1)
          matches (re-seq pattern text)]
      (if (empty? matches)
        [text]
        (remove nil?
                (mapcat
                 (fn [i part]
                   (let [url (nth matches i nil)]
                     (if url
                       [part [:a {:href url :target "_blank" :rel "noopener noreferrer"} url]]
                       [part])))
                 (range (count parts))
                 parts))))))



(def block-level-tags #{:p :div :h1 :h2 :h3 :h4 :h5 :h6 :blockquote :li :tr})

(defn hiccup->text [node]
  (cond
    (string? node) node
    (vector? node)
    (let [tag (first node)
          has-attrs? (map? (second node))
          attrs (if has-attrs? (second node) {})
          children (if has-attrs? (drop 2 node) (drop 1 node))
          inner-text (str/join "" (map hiccup->text children))]
      (cond
        (= tag :br) "\n"
        (contains? block-level-tags tag) (str inner-text "\n")
        (or (:mxc attrs) (= tag :img) (= tag "media-native")) " [e] "
        :else inner-text))
    (sequential? node) (str/join "" (map hiccup->text node))
    :else ""))


(defn process-raw-event [e source room-id]
  (let [html-txt     (or (get-in e [:content :inner :content :html]) "")
        cleaned-html (if (seq html-txt)
                       (str/replace html-txt #"(?i)<br[^>]*>\s*(?=</(?:p|div|h[1-6]|blockquote|li)>)" "")
                       "")
        safe-hiccup  (when (seq cleaned-html)
                       (sanitize-custom-html cleaned-html))
        plain-text   (if safe-hiccup
                       (hiccup->text safe-hiccup)
                       (or (:body e)
                           (get-in e [:content :body])
                           (get-in e [:content :inner :content :body])
                           (get-in e [:content :caption])
                           (get-in e [:content :inner :content :caption])
                           ""))]
    (-> e
        (assoc :timeline-source source)
        (assoc :room-id room-id)
        (assoc :clean-hiccup safe-hiccup)
        (assoc :clean-text plain-text)
        (update :type keyword)
        (assoc :raw e))))


(defn extract-metadata [raw-file]
  (p/create
   (fn [resolve-fn _]
     (let [mime      (.-type raw-file)
           is-image? (str/starts-with? mime "image/")
           is-video? (str/starts-with? mime "video/")
           reader    (js/FileReader.)
           att-id    (str (random-uuid))]
       (set! (.-onload reader)
             (fn [e]
               (let [raw-ab    (.. e -target -result)
                     ab-size   (.-byteLength raw-ab)
                     sab       (mesh/register-buffer! att-id ab-size)
                     sab-view  (js/Uint8Array. sab)
                     temp-view (js/Uint8Array. raw-ab)
                     _         (.set sab-view temp-view)
                     base-att  {:id          att-id
                                :buffer      {:mesh/buffer-id att-id}
                                :mime        mime
                                :filename    (.-name raw-file)
                                :size        (.-size raw-file)
                                :preview-url (js/URL.createObjectURL raw-file)}]
                 (if (or is-image? is-video?)
                   (let [el  (if is-image? (js/Image.) (js/document.createElement "video"))
                         url (js/URL.createObjectURL raw-file)]
                     (if is-image?
                       (set! (.-onload el)
                             (fn []
                               (js/URL.revokeObjectURL url)
                               (resolve-fn (assoc base-att :width (.-width el) :height (.-height el)))))
                       (set! (.-onloadedmetadata el)
                             (fn []
                               (js/URL.revokeObjectURL url)
                               (resolve-fn (assoc base-att :width (.-videoWidth el) :height (.-videoHeight el))))))
                     (set! (.-src el) url))
                   (resolve-fn base-att)))))
       (.readAsArrayBuffer reader raw-file)))))

(defonce relative-formatter
  (js/Intl.RelativeTimeFormat. js/undefined #js {:numeric "auto"}))

(defonce date-formatter
  (js/Intl.DateTimeFormat. js/undefined #js {:month "long"
                                             :day "numeric"
                                             :year "numeric"}))

(defn format-divider-date [ts]
  (let [date         (js/Date. ts)
        today        (js/Date.)
        is-today     (= (.toDateString date) (.toDateString today))
        yesterday    (doto (js/Date.) (.setDate (- (.getDate today) 1)))
        is-yesterday (= (.toDateString date) (.toDateString yesterday))]
    (cond
      is-today     (.format relative-formatter 0 "day")
      is-yesterday (.format relative-formatter -1 "day")
      :else        (.format date-formatter date))))

(defn format-time [ts]
  (when ts
    (let [date (js/Date. ts)]
      (.toLocaleTimeString date js/undefined #js {:hour "numeric" :minute "2-digit"}))))

(defn truncate-name [name max-len]
  (if (> (count name) max-len)
    (str (subs name 0 max-len) "...")
    name))

(defn join-names [names]
  (let [cnt (count names)]
    (case cnt
      0 ""
      1 (first names)
      2 (str (first names) " and " (second names))
      3 (str (first names) ", " (second names) ", and " (first (last names)))
      (str (first names) ", " (second names) ", and " (- cnt 2) " others"))))


(defn format-readers [names]
  (let [joined (join-names names)]
    (if (empty? joined)
      nil
      (str joined (if (= 1 (count names))
                    " is following the conversation"
                    " are following")))))

(defn get-status-string [tr type names]
  (let [cnt (count names)
        base-path (if (= type :typing)
                    "paradise.ui.container.timeline.status.typing"
                    "paradise.ui.container.timeline.status.reading")]
    (case cnt
      0 ""
      1 (tr [(keyword base-path "one")] [(truncate-name (first names) 16)])
      2 (tr [(keyword base-path "two")] [(truncate-name (first names) 16) (truncate-name (second names) 16)])
      3 (tr [(keyword base-path "three")] [(truncate-name (first names) 16) (truncate-name (second names) 16) (truncate-name (nth names 2) 16)])
      (tr [(keyword base-path "many")] [(truncate-name (first names) 16) (truncate-name (second names) 16) (- cnt 2)]))))

(defn fetch-state-event [homeserver token room-id event-type state-key]
  (let [clean-hs (str/replace homeserver #"/+$" "")
        key-path (if (empty? state-key) "" (str "/" state-key))
        url      (str clean-hs "/_matrix/client/v3/rooms/" room-id "/state/" event-type key-path)]
    (-> (p/let [resp (net/fetch url #js {:headers #js {:Authorization (str "Bearer " token)}})]
          (when (.-ok resp)
            (.json resp)))
        (p/catch (constantly nil)))))

(defn fetch-room-state
  "Fetches room state and applies an optional transformation function (predicate/transducer).
   - If event-type is nil, fetches the full state array.
   - xf is a function that receives the clojurized data."
  ([homeserver token room-id]
   (fetch-room-state homeserver token room-id nil nil identity))
  ([homeserver token room-id event-type state-key xf]
   (let [clean-hs (str/replace homeserver #"/+$" "")
         key-path (if (empty? state-key) "" (str "/" state-key))
         url      (str clean-hs "/_matrix/client/v3/rooms/" room-id "/state"
                       (when event-type (str "/" event-type key-path)))]
     (-> (p/let [resp (net/fetch url #js {:headers #js {:Authorization (str "Bearer " token)}})]
           (when (.-ok resp)
             (p/let [json (.json resp)
                     data (js->clj json :keywordize-keys true)]
               (xf data))))
         (p/catch (fn [err]
                    (log/error "State fetch failed:" url err)
                    nil))))))